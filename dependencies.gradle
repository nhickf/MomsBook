def dependencyGroup(Closure closure) {
    closure.delegate = dependencies
    return closure
}

ext {
    androidMinSdkVersion = 24
    androidTargetSdkVersion = 33
    androidCompileSdkVersion = 33

    activity_compose_version = '1.6.1'
    compose_version = '1.3.3'
    compose_tooling_version = '1.3.3'
    core_version = '1.9.0'
    lifecycle_version = '2.5.1'
    material3_version = '1.1.0-alpha03'
    room_version = '2.4.3'
    nav_version = '2.5.1'
    pager = '0.27.0'
    coroutines = '1.6.4'
    koin_version = '3.3.2'
    koin_android_version = '3.3.2'
    koin_android_compose_version = '3.4.1'
}

ext.appDependencies = dependencyGroup {
    implementation project(':features:listing')
    coreDependencies()
    composeDependencies()
    navigationDependencies()
    koinDependencies()
}

ext.featureListingDependencies = dependencyGroup {
    implementation project(':features:core')
    coreDependencies()
    composeDependencies()
    viewModelDependencies()
    navigationDependencies()
    koinDependencies()
}

ext.featureCoreDependencies = dependencyGroup {
    coreDependencies()
    koinDependencies()
    roomDependencies()
}

ext.coreDependencies = dependencyGroup {
    implementation "androidx.core:core-ktx:$core_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.compose.material3:material3:$material3_version"
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"

    // Koin Test features
    testImplementation "io.insert-koin:koin-test:$koin_version"

// Koin for JUnit 4
    testImplementation "io.insert-koin:koin-test-junit4:$koin_version"
// Koin for JUnit 5
    testImplementation "io.insert-koin:koin-test-junit5:$koin_version"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines"
}

ext.composeDependencies = dependencyGroup {
    implementation "androidx.activity:activity-compose:$activity_compose_version"
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.animation:animation:$compose_version"

    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"

    implementation "com.google.accompanist:accompanist-pager:$pager"
    implementation "com.google.accompanist:accompanist-pager-indicators:$pager"
}

ext.roomDependencies = dependencyGroup {
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
}

ext.navigationDependencies = dependencyGroup {
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    implementation "androidx.navigation:navigation-compose:$nav_version"
}

ext.viewModelDependencies = dependencyGroup {
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycle_version"
}

ext.koinDependencies = dependencyGroup {
    implementation "io.insert-koin:koin-core:$koin_version"

    implementation "io.insert-koin:koin-android:$koin_android_version"
// Java Compatibility
    implementation "io.insert-koin:koin-android-compat:$koin_android_version"
// Jetpack WorkManager
    implementation "io.insert-koin:koin-androidx-workmanager:$koin_android_version"
// Navigation Graph
    implementation "io.insert-koin:koin-androidx-navigation:$koin_android_version"
    // Jetpack Compose
    implementation "io.insert-koin:koin-androidx-compose:$koin_android_compose_version"
}
